pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()

        maven {
            name("FabricMC Maven")
            url("https://maven.fabricmc.net")
        }

        maven {
            name("Jitpack")
            url("https://jitpack.io")
        }

        maven { url "https://maven.architectury.dev/" }
        maven { url "https://files.minecraftforge.net/maven/" }
    }

    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": {
                    useModule("com.github.Hendrix-Shen:preprocessor:${requested.version}")
                    break
                }
            }
        }
    }
}

def versions = Arrays.asList(
        "1.16.5",
        "1.18.2",
        "1.19.2",
        "1.20.1"
)

for (String version : versions) {
    include(":$version")

    def proj = project(":$version")
    proj.projectDir = file("versions/$version")
    proj.buildFileName = "../../common.gradle"
}

include(":forgeWrapper")
